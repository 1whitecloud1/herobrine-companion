buildscript {
    repositories {
        // 优先检查本地和国内镜像
        mavenLocal()
        maven { url = uri("https://maven.aliyun.com/repository/public/") }
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0.16,6.2)', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'java'

version = mod_version
group = mod_group_id
archivesBaseName = mod_id


java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods { "${mod_id}" { source sourceSets.main } }
        }
        client { property 'forge.enabledGameTestNamespaces', mod_id }
        server { property 'forge.enabledGameTestNamespaces', mod_id; args '--nogui' }
        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "herobrine_companion.refmap.json"
    config "herobrine_companion.mixins.json"
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    flatDir {
        dir 'libs'
    }
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
        content {
            includeGroup "mezz.jei"
            includeGroup "vazkii.patchouli"
        }
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
        content {
            includeGroup "mezz.jei"
            includeGroup "vazkii.patchouli"
        }
    }
    maven {
        url "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven {
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }
    maven {
        url = "https://maven.createmod.net"
        content {
            includeGroup "net.createmod.ponder"
            includeGroup "dev.engine-room.flywheel"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
        content {
            includeGroup "dev.emi"
        }
    }

    // 必须让 Gradle 优先在这里找 Architectury，而不是去阿里云找
    maven { url = "https://maven.architectury.dev/" }

    // KubeJS 官方库
    maven { url = "https://maven.saps.dev/minecraft" }

    // 3. Minecraft 官方库
    maven {
        name = 'Minecraft'
        url = 'https://libraries.minecraft.net/'
    }

    // 4. Forge 官方库
    maven { url = 'https://maven.minecraftforge.net/' }

    // CurseMaven
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    // 6. 中央仓库
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // H2 Database (本地 libs)
    implementation files('libs/h2-2.2.224.jar')

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:15.20.0.106")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-forge-api:15.20.0.106")
    // runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.20.0.106")

    // Architectury
    implementation fg.deobf("dev.architectury:architectury-forge:9.2.14")

    // === KubeJS & Rhino ===
    implementation fg.deobf("dev.latvian.mods:kubejs-forge:2001.6.5-build.16")
    implementation fg.deobf("dev.latvian.mods:rhino-forge:2001.2.3-build.10")
}

// 资源替换逻辑
tasks.named('processResources', ProcessResources).configure {
    def replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) { expand(replaceProperties) }
    exclude 'herobrine_companion.mixins.json'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// 复制 Mixin 配置
task copyMixins(type: Copy) {
    from 'src/main/resources/herobrine_companion.mixins.json'
    into 'build/resources/main'
}
processResources.finalizedBy(copyMixins)

// === Jar 构建逻辑 (包含 H2 植入) ===
tasks.named('jar', Jar).configure {
    dependsOn 'copyMixins'
    manifest {
        attributes([
                "Specification-Title": mod_id,
                "Specification-Vendor": mod_authors,
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": project.jar.archiveVersion,
                "Implementation-Vendor": mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "herobrine_companion.mixins.json"
        ])
    }
    // 植入 H2
    from {
        files('libs/h2-2.2.224.jar').collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', 'module-info.class'
    
    // 排除被审核标记为敏感的 H2 工具类
    exclude 'org/h2/tools/Upgrade.class'
    exclude 'org/h2/tools/Shell.class'
    exclude 'org/h2/tools/RunScript.class'
    exclude 'org/h2/tools/Console.class'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    finalizedBy 'reobfJar'
}

// 避免编码错误
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}